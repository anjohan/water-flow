vx_filename = "output_vx_vs_x.dat"
vx_wrapped_filename = "output_vx_vs_x_wrapped.dat"
vx_asymm_filename = "output_asymmetry.dat"

set grid
set ylabel "vx [Å/ps]"
set key autotitle columnhead

set xlabel "x [Å]"

stats vx_filename skip 1 nooutput

set yrange[-5:50]

plot for[i=2:STATS_columns] vx_filename u 1:i w l lw 2
pause -1

stats vx_wrapped_filename skip 1 nooutput

set xlabel "|x-Lx/2| [Å]"
plot for[i=2:STATS_columns] vx_wrapped_filename u 1:i w l lw 2
pause -1

set multiplot layout 4, 1 title "Various measures of asymmetry"

f(x) = a*x+b
fit f(x) vx_asymm_filename u 1:3 via a, b

set xlabel "F [eV/Å]"
set ylabel "||vx(front) - vx(rear)||/||vx(rear)||"
unset yrange
set key bottom right
plot vx_asymm_filename u 1:3 title "||vx(front) - vx(rear)||/||vx(rear)||" w l lw 2, \
    a*x+b title sprintf("%.2g * F + %.2g", a, b) w l lw 2
!pause -1


set logscale xy

f(x) = a*x+b
fit f(x) vx_asymm_filename u (log($2)):(log($3)) via a, b

set xlabel "<vx> [Å/ps]"
set ylabel "||vx(front) - vx(rear)||/||vx(rear)||"
unset yrange
set key bottom right
plot vx_asymm_filename u 2:3 title "||vx(front) - vx(rear)||/||vx(rear)||" w l lw 2, \
    exp(b)*x**a title sprintf("<vx> ** %.2g", a) w l lw 2
!pause -1

f(x) = a*x+b
fit f(x) vx_asymm_filename u (log($1)):(log($4)) via a, b

set xlabel "F [eV/Å]"
set ylabel "int(vx(front) - vx(rear))/int(vx(rear))"
unset yrange
set key bottom right
plot vx_asymm_filename u 1:4 title "int(vx(front) - vx(rear))/int(vx(rear))" w l lw 2, \
    exp(b)*x**a title sprintf("F ** %.2g", a) w l lw 2
!pause -1

f(x) = a*x+b
fit f(x) vx_asymm_filename u (log($2)):(log($4)) via a, b
set xlabel "<vx> [Å/ps]"
set ylabel "int(vx(front) - vx(rear))/int(vx(rear))"
unset yrange
set key bottom right
plot vx_asymm_filename u 2:4 title "int(vx(front) - vx(rear))/int(vx(rear))" w l lw 2, \
    exp(b)*x**a title sprintf("<vx> ** %.2g", a) w l lw 2
!pause -1
unset multiplot
pause -1
